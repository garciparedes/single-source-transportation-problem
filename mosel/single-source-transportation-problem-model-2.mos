model "single-source-transportation-model-2"

    !----------------------------------------------------------------------------------------
    ! Single Source Transportation - Model 2
    ! Sergio García Prado - garciparedes.me
    ! April 2018
    !----------------------------------------------------------------------------------------

    uses "mmxprs";
    
    
    declarations
        n, m: integer
    end-declarations
    
    initializations from "data.dat"
        n m
    end-initializations

    declarations
        origins = 1..m
        destinations = 1..n
        
        offer: array(origins) of real
        demand: array(destinations) of real
        
        cost: array(origins, destinations) of real
        
        y: array(origins, destinations) of mpvar
    end-declarations
    
    initializations from "data.dat"
        offer demand cost
    end-initializations
    
    forall(i in origins, j in destinations) do
        y(i, j) is_binary
    end-do
    
    
    forall(i in origins) do
        res_ori(i) := sum(j in destinations) demand(j) * y(i, j) <= offer(i)
    end-do
    
    forall(j in destinations) do
        res_dest(j) := sum(i in origins) y(i, j) = 1
    end-do
    
    objetive := sum(i in origins, j in destinations) demand(j) * y(i, j)* cost(i, j)
    
    minimize(objetive)
    
    
    writeln("objetive = ", getobjval)
    
    writeln
    forall(i in origins) do
        writeln
        forall(j in destinations) do
            write(getsol(y(i,j)) * demand(j), "\t")
        end-do
    end-do

end-model
